plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

group "guru.qa"
version "1.0-SNAPSHOT"

def     selenideVersion = "7.0.6",
        slf4jVersion = "2.0.9",
        junitVersion = "5.10.1",
        allureVersion = "2.25.0"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion"
    )
    testRuntimeOnly (
            "org.slf4j:slf4j-simple:$slf4jVersion")
}


tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

tasks.register("web_tests", Test) {
    useJUnitPlatform {
        includeTags("WEB")
    }
}

tasks.register("smoke_tests", Test) {
    useJUnitPlatform {
        includeTags("SMOKE")
    }
}